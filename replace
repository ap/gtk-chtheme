#!/usr/bin/perl
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

use strict;
use warnings;

sub readconf {
	local @ARGV = @_;
	my @def;
	while(<>) {
		while(s/\\$//) {
			my $continuation = <>;
			last if not defined $continuation;
			$_ .= $continuation;
		}
		chomp;
		push @def, /\A \s* (\w+) \s* = \s* (.*)/sx;
	}
	return @def;
}

my %conf = readconf(shift);

my $match_key = do {
	local $_ = join '|', keys %conf;
	qr/!!($_)/;
};

while(<>) {
	1 while s/$match_key/$conf{$1}/e;
	print;
}
